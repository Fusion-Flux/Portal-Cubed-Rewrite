plugins {
	id "org.quiltmc.loom" version "1.6.+"
	id "maven-publish"
}

archivesBaseName = archives_base_name
group = maven_group
version = mod_version

repositories {
	maven { url = "https://api.modrinth.com/maven" }
	maven { url = "https://cursemaven.com" }
	maven { url = "https://maven.terraformersmc.com/releases/" }
	maven { url = "https://maven.shedaniel.me/" }
}

dependencies {
	// setup
	minecraft("com.mojang:minecraft:$minecraft_version")
	mappings loom.layered {
		it.mappings("org.quiltmc:quilt-mappings:$minecraft_version+build.$qm_build:intermediary-v2")
		it.officialMojangMappings { nameSyntheticMembers = false }
	}
	modImplementation("org.quiltmc:quilt-loader:$loader_version")

	// dependencies
	modImplementation("org.quiltmc.quilted-fabric-api:quilted-fabric-api:$qfapi_version")
	modImplementation("com.terraformersmc.terraform-api:terraform-wood-api-v1:$terraform_wood_api_version")

	// dev env
	modLocalRuntime("com.terraformersmc:modmenu:$mod_menu_version")
	modLocalRuntime("dev.emi:emi-fabric:$emi_version")
}

configurations.configureEach {
	exclude(group: "net.fabricmc", module: "fabric-loader")
	exclude(group: "net.fabricmc.fabric-api")
}

tasks.register("buildOrPublish") {
	group = "build"
	String mavenUser = System.getenv("MAVEN_USER")
	if (mavenUser != null && !mavenUser.isEmpty()) {
		dependsOn(tasks.named("publish"))
		println("prepared for publish")
	} else {
		dependsOn(tasks.named("build"))
		println("prepared for build")
	}
}

processResources {
	inputs.properties 'version': version, "minecraft_version": minecraft_version, "loader_version": loader_version, "qfapi_version": qfapi_version, "terraform_wood_api_version": terraform_wood_api_version

	filesMatching("quilt.mod.json") {
		expand inputs.properties
	}
}

sourceSets {
	testmod {
		compileClasspath += main.compileClasspath
		compileClasspath += main.output
		runtimeClasspath += main.runtimeClasspath
		runtimeClasspath += main.output
	}
}

loom {
	accessWidenerPath = file("src/main/resources/portalcubed.accesswidener")

	mods {
		"portalcubed" {
			sourceSet("main")
		}
	}

	runs {
		datagen {
			client()

			name "Minecraft Data"
			vmArg "-Dfabric-api.datagen"
			vmArg "-Dfabric-api.datagen.output-dir=${file("src/generated/resources")}"
			vmArg "-Dfabric-api.datagen.modid=portalcubed"
		}
		gametest {
			server()
			name "Test"
			source sourceSets.testmod
			vmArg "-Dfabric-api.gametest"
			vmArg "-Dfabric-api.gametest.report-file=${project.buildDir}/junit.xml"
			runDir "run/gametest_server"
		}

		configureEach {
			vmArg("-Dmixin.debug.export=true")
		}
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.encoding = "UTF-8"
	it.options.release.set(17)
}

java {
	sourceCompatibility = JavaVersion.VERSION_17
	targetCompatibility = JavaVersion.VERSION_17

	withSourcesJar()
}

jar {
	from("LICENSE") {
		rename { "${it}_${archivesBaseName}" }
	}
}

publishing {
	publications {
		register("mavenJava", MavenPublication) {
			artifactId = archivesBaseName + "_${project.name}"
			//noinspection GroovyAssignabilityCheck
			from(components.java)
		}
	}

	repositories {
		maven {
			url = "https://mvn.devos.one/${System.getenv("PUBLISH_SUFFIX")}/"
			credentials {
				username = System.getenv("MAVEN_USER")
				password = System.getenv("MAVEN_PASS")
			}
			authentication {
				create("basic", BasicAuthentication)
			}
		}
	}
}
